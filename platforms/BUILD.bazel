load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_zig//zig:defs.bzl", "zig_library")

PLATFORMS = {
    "cpu": True,
    "cuda": False,
    "rocm": False,
    "tpu": False,
    "neuron": False,
}

[
    bool_flag(
        name = runtime,
        build_setting_default = default,
    )
    for runtime, default in PLATFORMS.items()
]

[
    config_setting(
        name = "{}.enabled".format(runtime),
        flag_values = {":{}".format(runtime): "True"},
        visibility = ["//visibility:public"],
    )
    for runtime in PLATFORMS.keys()
]

zig_library(
    name = "platforms",
    main = "platforms.zig",
    visibility = ["//visibility:public"],
    deps = [
        "//pjrt",
    ] + [
        "//platforms/{}".format(runtime)
        for runtime in PLATFORMS.keys()
    ],
)

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
    flags = [
        "--force_pic=true",  # required for protobuf upb on Linux
    ],
    visibility = ["//visibility:public"],
)

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//cpu:aarch64",
        "@platforms//os:linux",
    ],
    flags = [
        "--force_pic=true",  # required for protobuf upb on Linux
    ],
    visibility = ["//visibility:public"],
)

platform(
    name = "macos_amd64",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:macos",
    ],
    visibility = ["//visibility:public"],
)

platform(
    name = "macos_arm64",
    constraint_values = [
        "@platforms//cpu:aarch64",
        "@platforms//os:macos",
    ],
    visibility = ["//visibility:public"],
)
