load("@aspect_bazel_lib//lib:copy_to_directory.bzl", "copy_to_directory")
load("@zml//bazel:patchelf.bzl", "patchelf")
load("@rules_cc//cc:cc_library.bzl", "cc_library")

patchelf(
    name = "libpjrt_cuda_so",
    src = "libpjrt_cuda.so",
    add_needed = [
        "libzmlxcuda.so.0",
    ],
    rename_dynamic_symbols = {
        "dlopen": "zmlxcuda_dlopen",
    },
    replace_needed = {
        "nvshmem_transport_ibrc.so.3": "nvshmem_transport_ibrc.so.4",
        "libnvrtc-builtins.so.13.0": "libnvrtc-builtins.so.13.1",
    },
    set_rpath = "$ORIGIN",
)

copy_to_directory(
    name = "sandbox",
    srcs = [
        ":libpjrt_cuda_so",
        "@cuda_cudart",
        "@cuda_cupti",
        "@cuda_nvcc",
        "@libnvvm",
        "@cuda_nvrtc",
        "@cuda_nvtx",
        "@cudnn",
        "@libnvshmem",
        "@libcublas",
        "@libcufft",
        "@libcusolver",
        "@libcusparse",
        "@libnvjitlink",
        "@nccl",
        "@zlib1g",
        "@zml//platforms/cuda:zmlxcuda",
    ],
    replace_prefixes = {
        "nvidia/nccl/lib": "lib",
        "nvvm/lib64": "lib",
        "libpjrt_cuda_so": "lib",
        "lib/x86_64-linux-gnu": "lib",
        "platforms/cuda": "lib",
    },
    add_directory_to_runfiles = False,
    include_external_repositories = ["**"],
)

cc_library(
    name = "libpjrt_cuda",
    data = [":sandbox"],
    deps = [
        "@cuda_cudart//:cuda",
    ],
    linkopts = [
        # Defer function call resolution until the function is called
        # (lazy loading) rather than at load time.
        #
        # This is required because we want to let downstream use weak CUDA symbols.
        #
        # We force it here because -z,now (which resolve all symbols at load time),
        # is the default in most bazel CC toolchains as well as in certain linkers.
        "-Wl,-z,lazy",
    ],
    visibility = ["@zml//platforms/cuda:__subpackages__"],
)
