module(
    name = "zml",
)

bazel_dep(name = "apple_support", version = "1.24.5")
bazel_dep(name = "abseil-cpp", version = "20250814.0")
bazel_dep(name = "aspect_bazel_lib", version = "2.21.2")
bazel_dep(name = "aspect_rules_py", version = "1.6.3")
bazel_dep(name = "bazel_skylib", version = "1.8.2")
bazel_dep(name = "libxev", version = "0.0.0-20251014.0-9f785d2")
bazel_dep(name = "patchelf", version = "0.18.0.bcr.1")
bazel_dep(name = "pcre2", version = "10.45")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "protobuf", version = "33.1", repo_name = "com_google_protobuf")

# Needs to be added before rules_cc so that the cc toolchain declared by
# apple_support wins over the one in rules_cc.
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "rules_distroless", version = "0.5.1")
bazel_dep(name = "rules_license", version = "1.0.0")
bazel_dep(name = "rules_oci", version = "2.2.6")
bazel_dep(name = "rules_proto", version = "7.1.0")
bazel_dep(name = "rules_python", version = "1.8.0-rc1")
bazel_dep(name = "rules_rust", version = "0.68.1")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "rules_zig", version = "0.12.2")
bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.3.1")
bazel_dep(name = "with_cfg.bzl", version = "0.11.0")

bazel_dep(name = "buildifier_prebuilt", version = "8.2.0.2", dev_dependency = True)

### Zig toolchain
zig = use_extension("@rules_zig//zig:extensions.bzl", "zig")
zig.index(file = "//bazel:zig_index.json")
zig.toolchain(zig_version = "0.16.0-dev.1912+0cbaaa5eb")
zig.mirrors(urls = [
    "https://mirror.zml.ai/zig",
    "https://ziglang.org/builds/",
])
use_repo(zig, "zig_toolchains")

register_toolchains("@rules_zig//zig/target:all")

register_toolchains("@zig_toolchains//:all")
###

### Apple C/C++ toolchain (needs to be first)
apple_cc_configure = use_extension("@apple_support//crosstool:setup.bzl", "apple_cc_configure_extension")
use_repo(apple_cc_configure, "local_config_apple_cc", "local_config_apple_cc_toolchains")

register_toolchains("@local_config_apple_cc_toolchains//:all")
###

### LLVM Bootstrapped toolchains
register_toolchains(
    "@toolchains_llvm_bootstrapped//toolchain:all",
)
###

uv = use_extension("@rules_python//python/uv:uv.bzl", "uv", dev_dependency = True)
uv.configure(version = "0.6.5")

python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(python_version = "3.12")
use_repo(python, "python_versions")

pip = use_extension("@rules_python//python/extensions:pip.bzl", "pip")
pip.parse(
    download_only = True,
    extra_pip_args = [
        "--abi=cp312",
        "--implementation=cp",
        "--python-version=312",
        "--platform=linux_x86_64",
        "--platform=manylinux2014_x86_64",
        "--platform=manylinux_2_27_x86_64",
        "--platform=manylinux_2_28_x86_64",
        "--platform=manylinux_2_29_x86_64",
    ],
    hub_name = "neuron_py_deps",
    python_version = "3.12",
    requirements_lock = "//platforms/neuron:requirements.lock.txt",
)
use_repo(pip, "neuron_py_deps")

# For downloading models
pip.parse(
    download_only = True,
    hub_name = "huggingface_hub",
    python_version = "3.12",
    requirements_lock = "//tools/hf:requirements.lock.txt",
)
use_repo(pip, "huggingface_hub")

cpu = use_extension("//platforms/cpu:cpu.bzl", "cpu_pjrt_plugin")
use_repo(cpu, "libpjrt_cpu_darwin_amd64", "libpjrt_cpu_darwin_arm64", "libpjrt_cpu_linux_amd64")


cuda = use_extension("//platforms/cuda:cuda.bzl", "cuda_packages")
use_repo(cuda, "libpjrt_cuda", "cuda_cudart")



rocm = use_extension("//platforms/rocm:rocm.bzl", "rocm_packages")
use_repo(rocm, "hipblaslt", "libpjrt_rocm", "rocblas")

tpu = use_extension("//platforms/tpu:tpu.bzl", "tpu_packages")
use_repo(tpu, "libpjrt_tpu")

neuron = use_extension("//platforms/neuron:neuron.bzl", "neuron_packages")
use_repo(neuron, "aws-neuronx-collectives", "aws-neuronx-runtime-lib", "libgomp1", "libpjrt_neuron", "zlib1g")

zls = use_extension("//third_party/zls:zls_toolchain.bzl", "zls_toolchains")
use_repo(zls, "zls_toolchains")

register_toolchains("@zls_toolchains//:all")

non_module_deps = use_extension("//:third_party/non_module_deps.bzl", "non_module_deps")
use_repo(non_module_deps, "arocc", "com_google_sentencepiece", "flashattn", "linenoise", "mnist", "org_swig_swig", "translate-c", "xla")



xla = use_extension("//third_party/xla:xla.bzl", "xla")
use_repo(
    xla,
    "com_github_grpc_grpc",
    "llvm-raw",
    "local_config_cuda",
    "local_config_remote_execution",
    "local_config_rocm",
    "local_config_sycl",
    "local_config_tensorrt",
    "python_version_repo",
    "rules_ml_toolchain",
    "stablehlo",
    "sycl_configure",
    "sycl_configure_ext",
    "triton",
    "tsl",
)

llvm = use_extension("//third_party/xla:llvm.bzl", "llvm")
llvm.configure()
use_repo(llvm, "llvm-project")

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = [
        "aarch64-apple-darwin",
        "x86_64-apple-darwin",
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
    versions = ["1.92.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//zml/tokenizer/hftokenizers:Cargo.lock",
    manifests = ["//zml/tokenizer/hftokenizers:Cargo.toml"],
    supported_platform_triples = [
        "aarch64-apple-darwin",
        "x86_64-apple-darwin",
        "aarch64-unknown-linux-gnu",
        "x86_64-unknown-linux-gnu",
    ],
)
use_repo(crate, "crates")

apt = use_extension("@rules_distroless//apt:extensions.bzl", "apt")
apt.install(
    name = "apt_cuda",
    lock = "//platforms/cuda:packages.lock.json",
    manifest = "//platforms/cuda:packages.yaml",
)
use_repo(apt, "apt_cuda")

apt.install(
    name = "apt_rocm",
    lock = "//platforms/rocm:packages.lock.json",
    manifest = "//platforms/rocm:packages.yaml",
)
use_repo(apt, "apt_rocm")

apt.install(
    name = "apt_neuron",
    lock = "//platforms/neuron:packages.lock.json",
    manifest = "//platforms/neuron:packages.yaml",
)
use_repo(apt, "apt_neuron")

oci = use_extension("@rules_oci//oci:extensions.bzl", "oci")
oci.pull(
    name = "distroless_cc_debian12",
    digest = "sha256:eccec5274132c1be0ce5d2c8e6fe41033e64af5e987ccee9007826e4c012069d",
    image = "gcr.io/distroless/cc-debian12",
    platforms = ["linux/amd64"],
)
use_repo(oci, "distroless_cc_debian12", "distroless_cc_debian12_linux_amd64")

huggingface = use_extension("@zml//bazel:huggingface.bzl", "huggingface")

################################################################################
# ModernBERT
huggingface.model(
    name = "ModernBERT-base",
    build_file_content = """exports_files(glob(["**"]), visibility = ["//visibility:public"])""",
    commit = "94032bb66234a691cf6248265170006a7ced4970",
    includes = [
        "model.safetensors",
        "tokenizer.json",
    ],
    model = "answerdotai/ModernBERT-base",
)
huggingface.model(
    name = "ModernBERT-large",
    build_file_content = """exports_files(glob(["**"]), visibility = ["//visibility:public"])""",
    commit = "4bbcbf40bed02ce487125bcb3c897ea9bdc88340",
    includes = [
        "model.safetensors",
        "tokenizer.json",
    ],
    model = "answerdotai/ModernBERT-large",
)
use_repo(huggingface, "ModernBERT-base", "ModernBERT-large")
################################################################################

git_override(
    module_name = "rules_zig",
    commit = "b4ae496abfd61f880927dd01ea59823df7b42c29",
    remote = "https://github.com/zml/rules_zig.git",
)

git_override(
    module_name = "toolchains_llvm_bootstrapped",
    commit = "023f62a94eb38d2991432a766a1d4c05e90414d7",
    remote = "https://github.com/cerisier/toolchains_llvm_bootstrapped.git",
)

git_override(
    module_name = "apple_support",
    commit = "6ddcf9c1c46be1a7eb8767c7dbec7714dd47efb8",
    remote = "https://github.com/cerisier/apple_support.git",
)
