load("@bazel_skylib//rules:select_file.bzl", "select_file")
load("@python_versions//3.11:defs.bzl", "py_binary")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")
load("@rules_uv//uv:pip.bzl", "pip_compile")
load("@zml//bazel:cc_import.bzl", "cc_import")
load("@zml//bazel:pybootstrap.bzl", "python_bootstrap")
load("@zml//bazel:runfiles.bzl", "runfiles_to_default")

pip_compile(
    name = "update_requirements",
    args = [
        "--generate-hashes",
        "--emit-index-url",
        "--emit-find-links",
        "--no-strip-extras",
        "--index-strategy unsafe-best-match",
    ],
    python_platform = "x86_64-unknown-linux-gnu",
    requirements_in = "requirements.in",
    requirements_txt = "requirements.lock.txt",
)

py_console_script_binary(
    name = "neuronx-cc",
    binary_rule = py_binary,
    pkg = "@neuron_py_deps//neuronx_cc",
    precompile = "enabled",
    precompile_source_retention = "omit_source",
    pyc_collection = "include_pyc",
    visibility = ["//visibility:public"],
)

py_binary(
    name = "sandbox",
    srcs = ["empty.py"],
    main = "empty.py",
    precompile = "enabled",
    precompile_source_retention = "omit_source",
    pyc_collection = "include_pyc",
    deps = [
        ":neuronx-cc",
        "@neuron_py_deps//libneuronxla",
    ],
)

python_bootstrap(
    name = "python_bootstrap",
    deps = [":sandbox"],
)

cc_library(
    name = "libpython",
    hdrs = ["libpython.h"],
    visibility = ["//visibility:public"],
    deps = [
        "@rules_python//python/cc:current_py_cc_headers",
        "@rules_python//python/cc:current_py_cc_libs",
    ],
)

compile_pip_requirements(
    name = "requirements",
    src = "requirements.in",
    py_binary = py_binary,
    requirements_txt = "requirements_lock.txt",
)

runfiles_to_default(
    name = "libneuronxla_files",
    deps = ["@neuron_py_deps//libneuronxla:pkg"],
)

select_file(
    name = "libneuronpjrt_so",
    srcs = ":libneuronxla_files",
    subpath = "site-packages/libneuronxla/libneuronpjrt.so",
)

cc_import(
    name = "libpjrt_neuron",
    add_needed = [
        "libpython3.11.so.1.0",
        "libzmlxneuron.so.0",
    ],
    data = [
        ":neuronx-cc",
        ":python_bootstrap",
        ":sandbox",
    ],
    shared_library = ":libneuronpjrt_so",
    soname = "libpjrt_neuron.so",
    deps = [
        ":zmlxneuron",
        "@aws-neuronx-runtime-lib",
        "@rules_python//python/cc:current_py_cc_libs",
    ],
)

cc_library(
    name = "zmlxneuron_lib",
    srcs = ["zmlxneuron.c"],
    linkopts = ["-ldl"],
)

cc_shared_library(
    name = "zmlxneuron_",
    shared_lib_name = "libzmlxneuron.so.0",
    deps = [":zmlxneuron_lib"],
)

cc_import(
    name = "zmlxneuron",
    shared_library = ":zmlxneuron_",
)

alias(
    name = "neuron",
    actual = "libpjrt_neuron",
    visibility = ["//visibility:public"],
)

alias(
    name = "bootstrap",
    actual = "@rules_python//python/config_settings:bootstrap_impl",
)
