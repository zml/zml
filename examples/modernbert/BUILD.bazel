load("@modernbert_pip//:requirements.bzl", "requirement")
load("@rules_python//python:py_binary.bzl", "py_binary")
load("@rules_uv//uv:pip.bzl", "pip_compile")
load("@zml//bazel:zig.bzl", "zig_cc_binary")

## TODO: Test for multiplatform : linux-amd64, mac-arm64, etc ..
pip_compile(
    name = "update-requirements-lock",
    requirements_in = "requirements.in",
    requirements_txt = "requirements.lock.txt",
)

py_binary(
    name = "activations",
    srcs = ["activations.py"],
    deps = [
        requirement("torch"),
        requirement("transformers"),
        requirement("accelerate"),
        requirement("numpy"),
        "@zml//tools:zml_utils",
    ],
)

filegroup(
    name = "modernbert_activations",
    srcs = [":activations"],
    output_group = "modernbert",
    visibility = ["//visibility:public"],
)

genrule(
    name = "modernbert_activations_file",
    srcs = [":activations"],
    outs = ["ModernBERT-base.activations.pt"],
    cmd = """
        BINARY="$(locations :activations)"
        FIRST_FILE=$$(echo $$BINARY | cut -d' ' -f1)
        $$FIRST_FILE && mv ModernBERT-base.activations.pt $@
    """,
    local = True,
    tools = [":activations"],
    visibility = ["//visibility:public"],
)

zig_cc_binary(
    name = "modernbert",
    srcs = ["modernbert.zig"],
    main = "main.zig",
    deps = [
        "//third_party/tigerbeetle:flags",
        "@zml//async",
        "@zml//stdx",
        "@zml//zml",
    ],
)

cc_binary(
    name = "ModernBERT-base",
    args = [
        "--model=$(location @ModernBERT-base//:model.safetensors)",
        "--tokenizer=$(location @ModernBERT-base//:tokenizer)",
        "--num-attention-heads=12",
    ],
    data = [
        "@ModernBERT-base//:model.safetensors",
        "@ModernBERT-base//:tokenizer",
    ],
    deps = [":modernbert_lib"],
)

cc_binary(
    name = "ModernBERT-large",
    args = [
        "--model=$(location @ModernBERT-large//:model.safetensors)",
        "--tokenizer=$(location @ModernBERT-large//:tokenizer)",
        "--num-attention-heads=16",
    ],
    data = [
        "@ModernBERT-large//:model.safetensors",
        "@ModernBERT-large//:tokenizer",
    ],
    deps = [":modernbert_lib"],
)

zig_cc_binary(
    name = "test-implementation",
    srcs = ["modernbert.zig"],
    args = [
        "--model=$(location @ModernBERT-base//:model.safetensors)",
        "--activations=$(location :modernbert_activations_file)",
    ],
    data = [
        ":modernbert_activations_file",
        "@ModernBERT-base//:model.safetensors",
    ],
    main = "test.zig",
    deps = [
        "//third_party/tigerbeetle:flags",
        "@zml//async",
        "@zml//zml",
    ],
)
